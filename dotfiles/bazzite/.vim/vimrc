"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Configuration de vim

" Paramétrage de base
syntax on                         " Active la colorisation syntaxique
set hlsearch                      " Affiche en surbrillance les recherches
set background=dark               " Optimise l'affiche pour un terminal sombre
set smartindent                   " Indentation intelligente
set smarttab                      " Gestion des espaces en début de ligne
set autoindent                    " Conserve l'indentation sur une nouvelle ligne
set ruler                         " Affiche la position du curseur
set tabstop=2                     " La largeur d'une tabulation est définie sur 2
set shiftwidth=2                  " Les retraits auront une largeur de 2
set softtabstop=2                 " Nombre de colonnes pour une tabulation
set expandtab                     " Remplace les tab par des espaces
set linebreak                     " Revient à la ligne sans couper les mots
set showcmd                       " Afficher la commande dans la ligne d'état
set showmatch                     " Afficher les parenthèses correspondantes
set ignorecase                    " Ignorer la casse
set smartcase                     " Faire un appariement intelligent
set incsearch                     " Recherche incrémentielle
set hidden                        " Cacher les tampons lorsqu'ils sont abandonnés
set mouse=                        " Désactive la souris par défaut
set nobackup                      " Désactive les sauvegardes automatiques
set spelllang=fr,en               " Spécifie les langues du dictionnaire
set viminfofile=~/.vim/.viminfo   " Change l'emplacement du fichier viminfo

" Permet l'indentation automatique : gg=G
filetype plugin indent on

" Définition des caractères invisibles
let &listchars = "eol:$,space:\u00B7"

" Changement automatique du curseur en fonction du mode
let &t_SI = "\e[6 q"
let &t_EI = "\e[2 q"

" Mémoriser la dernière position du curseur
autocmd BufReadPost * if (line("'\"") > 1) && (line("'\"") <= line("$")) | silent exe "silent! normal g'\"zO" | endif

" Désactivation des # au retour chariot
autocmd FileType * setlocal formatoptions-=c formatoptions-=r formatoptions-=o

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Explorateur de fichiers

" Configuration
let g:netrw_liststyle = 3         " Active le mode Treeview
let g:netrw_sizestyle = "H"       " Active le mode human-readable
let g:netrw_banner = 0            " Désactive la bannière
let g:netrw_browse_split = 4      " Ouvre le fichier choisi dans un panel
let g:netrw_winsize = 15          " Définit la taille de l'explorateur

" Ferme automatiquement l'explorateur
aug netrw_close
  au!
  au WinEnter * if winnr('$') == 1 && getbufvar(winbufnr(winnr()), "&filetype") == "netrw"|q|endif
aug END

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Fonctions

" Souris
function! ToggleMouse()
  if &mouse == 'a'
    set mouse=
    echo "Souris désactivée"
  else
    set mouse=a
    echo "Souris activée"
  endif
endfunction

" Mode IDE
function! ModeIDE()
  set number!
  call ToggleMouse()
endfunction

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Mapping

" Explorateur de fichiers
nnoremap <F1> <Cmd>Vexplore<CR>

" Mode IDE
nnoremap <F2> <Cmd>call ModeIDE()<CR>

" Correction orthographique (z= pour afficher les propositions)
map <F3> <Cmd>set spell!<CR>

" Affichage des caractères invisibles
nnoremap <F4> <Cmd>set list!<CR>

" Indentation automatique
nnoremap <F5> gg=G<CR>

" Changement de document
nnoremap <TAB> <Cmd>tabnext<CR>
nnoremap <S-TAB> <C-W>w

" Fermeture automatique des brackets
inoremap { {}<Esc>ha
inoremap [ []<Esc>ha

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Theme Catppuccin

if filereadable(expand("~/.vim/colors/catppuccin_macchiato.vim"))
  colorscheme catppuccin_macchiato
  set cursorline
  set termguicolors
endif
